## --- UTILS --- ##
import aviation:utils as utils
prefix = "aviation.plane"

## --- FUNCTIONS --- ##
function aviation:planes/move/loop:
    scoreboard players remove temp aviation.planes.velocity 1
    tp @s ^ ^ ^0.01
    utils.check_collision(1,1,3,"^")
    execute if score collisions aviation.planes.temp matches 1..:
        tp @s ^ ^ ^-0.01
        scoreboard players set temp aviation.planes.velocity 0
        scoreboard players set @s aviation.planes.velocity 0
        scoreboard players operation prev_cols aviation.planes.temp = collisions aviation.planes.temp
        utils.check_top_collisions(1,1,3,"^")
        execute if score collisions aviation.planes.temp < prev_cols aviation.planes.temp:
            for _ in range(10):
                summon creeper ~ ~ ~ {Fuse:0,CustomName:"a Plane Crash",powered:1b}
                execute on passengers:
                    if entity @s[tag=prefix] run kill @s
                kill @s
        execute if score collisions aviation.planes.temp = prev_cols aviation.planes.temp:
            tag @s add f"{prefix}.state.on_ground"
            tag @s remove f"{prefix}.state.in_air"
            scoreboard players set @s aviation.planes.pitch 0 
            scoreboard players set @s aviation.planes.roll 0 


    execute at @s if score temp aviation.planes.velocity matches 1.. run function aviation:planes/move/loop

function aviation:planes/move:
    scoreboard players operation temp aviation.planes.velocity = @s aviation.planes.velocity
    execute at @s run function aviation:planes/move/loop


function aviation:planes/new:
    execute summon interaction:
        tag @s add f"{prefix}.root"
        tag @s add f"{prefix}.state.on_ground"
        tag @s add prefix


        scoreboard players set @s aviation.planes.velocity 0
        scoreboard players set @s aviation.planes.pitch 0
        scoreboard players set @s aviation.planes.yaw 0
        scoreboard players set @s aviation.planes.roll 0



function aviation:planes/state_controller:
    execute on passengers:
        if entity @s[type=player,predicate=aviation:planes/accellerate] as @n[tag=f"{prefix}.self"]:
            scoreboard players add @s aviation.planes.velocity 100

    execute if entity @s[tag=f"{prefix}.state.in_air",tag=!f"{prefix}.state.on_ground"]:
        utils.scale_plane_value(value_score="aviation.planes.velocity",amount_id="ground_friction", amount_score="aviation.planes.constants")
        # -- Controls -- #
        scoreboard players remove @s aviation.planes.pitch 1
        execute on passengers:
            # - Pitch - #
            if entity @s[type=player,predicate=aviation:planes/pitch/up] as @n[tag=f"{prefix}.self"]:
                scoreboard players add @s aviation.planes.pitch 11
            if entity @s[type=player,predicate=aviation:planes/pitch/down] as @n[tag=f"{prefix}.self"]:
                scoreboard players remove @s aviation.planes.pitch 9
            # - Roll - #
            if entity @s[type=player,predicate=aviation:planes/roll/left] as @n[tag=f"{prefix}.self"]:
                scoreboard players add @s aviation.planes.roll 10
            if entity @s[type=player,predicate=aviation:planes/roll/right] as @n[tag=f"{prefix}.self"]:
                scoreboard players remove @s aviation.planes.roll 10

        execute if score @s aviation.planes.pitch matches 90001.. run scoreboard players set @s aviation.planes.pitch 90000
        execute if score @s aviation.planes.pitch matches ..-90001 run scoreboard players set @s aviation.planes.pitch -90000

        execute store result storage aviation.planes:temp controls.roll.scaled float 0.1 run scoreboard players get @s aviation.planes.roll
        execute store result score yaw_add aviation.planes.math run data get storage aviation.planes:temp controls.roll.scaled
        scoreboard players operation @s aviation.planes.yaw += yaw_add aviation.planes.math

    execute if entity @s[tag=f"{prefix}.state.on_ground",tag=!f"{prefix}.state.in_air"]:
        utils.scale_plane_value(value_score="aviation.planes.velocity",amount_id="ground_friction", amount_score="aviation.planes.constants")
        
        execute if score @s aviation.planes.velocity matches 500.. on passengers if entity @s[type=player,predicate=aviation:planes/pitch/up] as @n[tag=f"{prefix}.self"]:
            tag @s remove f"{prefix}.state.on_ground"
            tag @s add f"{prefix}.state.in_air"

    execute if score @s aviation.planes.velocity matches 1.. run function aviation:planes/move
    execute store result entity @s Rotation[1] float -0.01 run scoreboard players get @s aviation.planes.pitch
    execute store result entity @s Rotation[0] float -0.01 run scoreboard players get @s aviation.planes.yaw
        

function aviation:planes/tick:
    tag @s add f"{prefix}.self"
    execute if data entity @s interaction:
        execute on target run ride @s mount @n[tag=f"{prefix}.self"]
        data remove entity @s interaction

    function aviation:planes/state_controller
    tag @s remove f"{prefix}.self"



## --- PREDICATES --- ##
predicate aviation:planes/pitch/up {
	"condition": "minecraft:entity_properties",
	"entity": "this",
	"predicate": {
		"type_specific": {
			"type": "minecraft:player",
			"input": {
				"forward": true
			}
		}
	}
}

predicate aviation:planes/pitch/down {
	"condition": "minecraft:entity_properties",
	"entity": "this",
	"predicate": {
		"type_specific": {
			"type": "minecraft:player",
			"input": {
				"backward": true
			}
		}
	}
}

predicate aviation:planes/roll/left {
	"condition": "minecraft:entity_properties",
	"entity": "this",
	"predicate": {
		"type_specific": {
			"type": "minecraft:player",
			"input": {
				"left": true
			}
		}
	}
}

predicate aviation:planes/roll/right {
	"condition": "minecraft:entity_properties",
	"entity": "this",
	"predicate": {
		"type_specific": {
			"type": "minecraft:player",
			"input": {
				"right": true
			}
		}
	}
}

predicate aviation:planes/accellerate {
	"condition": "minecraft:entity_properties",
	"entity": "this",
	"predicate": {
		"type_specific": {
			"type": "minecraft:player",
			"input": {
				"jump": true
			}
		}
	}
}