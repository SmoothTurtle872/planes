## --- UTILS --- ##
import math
import aviation:utils as utils
prefix = "aviation.plane"


## --- FUNCTIONS --- ##
function aviation:planes/new:
    execute summon item_display:
        data modify entity @s item set value {id:"minecraft:popped_chorus_fruit",count:1,components:{"minecraft:item_model":"aviation:small_oak_biplane"}}
        data modify storage quack:api euler set value [0f, -6f, 0f]
        function quack:api/storage/euler_to_quaternion
        data modify entity @s transformation.left_rotation set from storage quack:api quaternion
        data merge entity @s {interpolation_duration:1,teleport_duration:1,transformation:{scale:[3,3,3]}}
        tp @s ~ ~1 ~
        tag @s add f"{prefix}.root"
        tag @s add f"{prefix}.self"
        tag @s add f"{prefix}.state.on_ground"
        tag @s add prefix

        execute summon interaction:
            tag @s add prefix
            tag @s add f"{prefix}.hitbox"
            ride @s mount @n[tag=f"{prefix}.self"]

        tag @s remove f"{prefix}.self"

        scoreboard players set @s aviation.planes.velocity 0
        scoreboard players set @s aviation.planes.pitch 0
        scoreboard players set @s aviation.planes.yaw 0
        scoreboard players set @s aviation.planes.roll 0
#=================================================================================
function aviation:planes/tick:
    tag @s add f"{prefix}.self"
    execute on passengers if entity @s[type=interaction] if data entity @s interaction:
        execute on target run ride @s mount @n[tag=f"{prefix}.self"]
        data remove entity @s interaction

    function aviation:planes/state_controller
    function aviation:planes/move/controller
    function aviation:planes/visuals/rotation
    tag @s remove f"{prefix}.self"
#=================================================================================
function aviation:planes/state_controller:   
    # -- States -- #
    execute if entity @s[tag=f"{prefix}.state.in_air",tag=!f"{prefix}.state.on_ground"] run function aviation:planes/states/air
    execute if entity @s[tag=f"{prefix}.state.on_ground",tag=!f"{prefix}.state.in_air"] run function aviation:planes/states/ground
#--------------------------------------------------------------------------------
function aviation:planes/states/air:
    # -- Air Controls -- #
    scoreboard players remove @s aviation.planes.pitch 1
    execute on passengers:
        # - Pitch - #
        if entity @s[type=player,predicate=aviation:planes/pitch/up] as @n[tag=f"{prefix}.self"]:
            scoreboard players add @s aviation.planes.pitch 31
        if entity @s[type=player,predicate=aviation:planes/pitch/down] as @n[tag=f"{prefix}.self"]:
            scoreboard players remove @s aviation.planes.pitch 29
        # - Roll - #
        if entity @s[type=player,predicate=aviation:planes/roll/left] as @n[tag=f"{prefix}.self"]:
            scoreboard players add @s aviation.planes.roll 30
        if entity @s[type=player,predicate=aviation:planes/roll/right] as @n[tag=f"{prefix}.self"]:
            scoreboard players remove @s aviation.planes.roll 30

    execute if score @s aviation.planes.pitch matches 90001.. run scoreboard players set @s aviation.planes.pitch 90000
    execute if score @s aviation.planes.pitch matches ..-90001 run scoreboard players set @s aviation.planes.pitch -90000

    execute store result storage aviation.planes:temp controls.roll.scaled float 0.01 run scoreboard players get @s aviation.planes.roll
    execute store result score yaw_add aviation.planes.math run data get storage aviation.planes:temp controls.roll.scaled
    scoreboard players operation @s aviation.planes.yaw += yaw_add aviation.planes.math
#---------------------------------------------------------------------------------
function aviation:planes/states/ground:
    execute if score @s aviation.planes.velocity matches 50.. on passengers if entity @s[type=player,predicate=aviation:planes/pitch/up] as @n[tag=f"{prefix}.self"]:
        tag @s remove f"{prefix}.state.on_ground"
        tag @s add f"{prefix}.state.in_air"
#=================================================================================
function aviation:planes/visuals/rotation:
    execute store result entity @s Rotation[1] float -0.01 run scoreboard players get @s aviation.planes.pitch
    execute store result entity @s Rotation[0] float -0.01 run scoreboard players get @s aviation.planes.yaw
    data modify storage quack:api quaternion[0] set value 0
    data modify storage quack:api quaternion[1] set value 0
    execute store result storage quack:api euler[2] float -0.01 run scoreboard players get @s aviation.planes.roll
    function quack:api/storage/euler_to_quaternion
    data modify entity @s transformation.left_rotation set from storage quack:api quaternion
#=================================================================================
function aviation:planes/move/controller:
    utils.scale_plane_value(value_score="aviation.planes.velocity",amount_id="ground_friction", amount_score="aviation.planes.constants")
    execute on passengers:
        if entity @s[type=player,predicate=aviation:planes/accellerate] as @n[tag=f"{prefix}.self"]:
            scoreboard players add @s aviation.planes.velocity 10
            execute if score @s aviation.planes.velocity matches 201.. run scoreboard players set @s aviation.planes.velocity 200
    execute if score @s aviation.planes.velocity matches 1.. run function aviation:planes/move/prep
#---------------------------------------------------------------------------------
function aviation:planes/move/prep:
    scoreboard players operation temp aviation.planes.velocity = @s aviation.planes.velocity
    execute at @s run function aviation:planes/move/loop
#---------------------------------------------------------------------------------
function aviation:planes/move/loop:
    scoreboard players remove temp aviation.planes.velocity 1
    tp @s ^ ^ ^0.1
    utils.check_collision(3,3,3,"^")
    execute if score collisions aviation.planes.temp matches 1..:
        tp @s ^ ^ ^-0.1
        scoreboard players set temp aviation.planes.velocity 0
        scoreboard players set @s aviation.planes.velocity 0
        execute positioned ~ ~-1 ~:
            execute positioned ~ ~2 ~:
                utils.check_collision(3,2,3,"^")
                execute if score collisions aviation.planes.temp matches 1..:
                    summon creeper ~ ~ ~ {powered:1b,CustomName:"a Plane Explosion", ExplosionRadius:10b,Fuse:0}
                    execute on passengers if entity @s[tag=prefix] run kill @s
                    kill @s
            utils.check_collision(3,1,3,"^",True)
            execute if score collisions aviation.planes.temp matches 1..:
                execute if entity @s[tag=f"{prefix}.state.on_ground"] at @s:
                    tp @s ~ ~1 ~
                execute if entity @s[tag=f"{prefix}.state.in_air"]:
                    tag @s remove f"{prefix}.state.in_air"
                    tag @s add f"{prefix}.state.on_ground"
                    scoreboard players set @s aviation.planes.pitch 0
                    scoreboard players set @s aviation.planes.roll 0


    execute at @s if score temp aviation.planes.velocity matches 1.. run function aviation:planes/move/loop

        


## --- PREDICATES --- ##
predicate aviation:planes/pitch/up {
	"condition": "minecraft:entity_properties",
	"entity": "this",
	"predicate": {
		"type_specific": {
			"type": "minecraft:player",
			"input": {
				"forward": true
			}
		}
	}
}

predicate aviation:planes/pitch/down {
	"condition": "minecraft:entity_properties",
	"entity": "this",
	"predicate": {
		"type_specific": {
			"type": "minecraft:player",
			"input": {
				"backward": true
			}
		}
	}
}

predicate aviation:planes/roll/left {
	"condition": "minecraft:entity_properties",
	"entity": "this",
	"predicate": {
		"type_specific": {
			"type": "minecraft:player",
			"input": {
				"left": true
			}
		}
	}
}

predicate aviation:planes/roll/right {
	"condition": "minecraft:entity_properties",
	"entity": "this",
	"predicate": {
		"type_specific": {
			"type": "minecraft:player",
			"input": {
				"right": true
			}
		}
	}
}

predicate aviation:planes/accellerate {
	"condition": "minecraft:entity_properties",
	"entity": "this",
	"predicate": {
		"type_specific": {
			"type": "minecraft:player",
			"input": {
				"jump": true
			}
		}
	}
}